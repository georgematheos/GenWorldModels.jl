/**
* Classic "balls in urn" example from, eg., Milch et al, AISTATS 2005.
* See `blog/example/balls/id-uncert-noisy.blog`.
*/

type Ball;
type Draw;
type Color;

random Color Color(Ball b) ~ UniformChoice({c for Color c});
random Ball BallDrawn(Draw d) ~ UniformChoice({b for Ball b});

random Boolean CorrectObservation(Draw d) ~ BooleanDistrib(0.9);

random Color ObsColor(Draw d) ~
    if CorrectObservation(d)
        then Color(BallDrawn(d))
        else UniformChoice({c for Color c});

distinct Color colors[10];
distinct Draw draws[6];
#Ball ~ Poisson(11);

obs ObsColor(draws[0]) = colors[0];
obs ObsColor(draws[1]) = colors[1];
obs  ObsColor(draws[2]) = colors[1];
obs ObsColor(draws[3]) = colors[4];
obs ObsColor(draws[4]) = colors[3];
obs  ObsColor(draws[5]) = colors[6];

query (BallDrawn(draws[1]) == BallDrawn(draws[2]));
query size({b for Ball b});